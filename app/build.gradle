
apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

def packageName = "co.to_kanzaki.highwayfee"

android {
    kapt {
        generateStubs = true
    }
    compileSdkVersion 26
    flavorDimensions "usage"
    defaultConfig {
        applicationId "co.to_kanzaki.highwayfee"
        minSdkVersion 23
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        // Enabling multidex support.
        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {

        prd {
            dimension "usage"
            applicationId packageName
            buildConfigField "String", "API_ROOT", "\"http://kosoku.jp/api/\""
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation"org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

    implementation 'com.android.support.constraint:constraint-layout:1.1.2'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'


    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //Anko
    implementation 'org.jetbrains.anko:anko:0.10.2'
    implementation 'org.jetbrains.anko:anko-sqlite:0.10.2'

    //stetho
    implementation 'com.facebook.stetho:stetho:1.5.0'

    //Dagger2
    def daggerVersion = '2.11'
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"

    //Retrofit
    def retrofitVersion = '2.3.0'
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofitVersion"

    implementation 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    //Moshi
    def moshi_version = '1.5.0'
    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation("com.squareup.moshi:moshi-kotlin:$moshi_version") {
        exclude group: "org.jetbrains.kotlin", module: "kotlin-reflect"
    }
    implementation "org.jetbrains.kotlin:kotlin-reflect:1.2.21"
    //32個以上パラメタがある場合にJSONから変換できない件対応
    //参考：https://github.com/square/moshi/issues/323

    //RxJava
    implementation 'io.reactivex.rxjava2:rxjava:2.1.9'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'

    //kvs-schema for sharedPreferences
    kapt 'com.rejasupotaro:kvs-schema-compiler:5.0.1'
    implementation 'com.rejasupotaro:kvs-schema:5.0.1'

    //simple(for xml parse)
    implementation 'org.simpleframework:simple-xml:2.7.1'
    implementation('com.squareup.retrofit2:converter-simplexml:2.0.0-beta4') {
        exclude module: 'stax'
        exclude module: 'stax-api'
        exclude module: 'xpp3'
    }
}
//依存ライブラリの指定するサポートライブラリバージョンがバラバラなため、ここで統一する
//https://stackoverflow.com/questions/42949974/android-support-repo-46-0-0-with-android-studio-2-3
configurations.all {
    resolutionStrategy.eachDependency { DependencyResolveDetails details ->
        def requested = details.requested
        if (requested.group == 'com.android.support') {
            if (!requested.name.startsWith("multidex")) {
                details.useVersion '26.1.0'
            }
        }
    }
}

